plugins {
    id "com.gradle.build-scan" version "1.15.2"
    id "org.sonarqube" version "2.6.2"
    id "com.github.kt3k.coveralls" version "2.6.3"
}

buildScan {
    termsOfServiceUrl = "https://gradle.com/terms-of-service"
    termsOfServiceAgree = "yes"
}

sonarqube {
    properties {
        property "sonar.projectName", "Australia Rent"
        property "sonar.projectKey", "thomashan.github.io:australia-rent"
    }
}

version project.VERSION

allprojects {
    apply plugin: "groovy"
    apply plugin: "eclipse"
    apply plugin: "idea"
    apply plugin: "jacoco"

    repositories {
        mavenLocal()
        mavenCentral()
    }

    sourceCompatibility = 1.9
    targetCompatibility = 1.9
    group "thomashan.github.io"

    dependencies {
        compile("org.codehaus.groovy:groovy-all:2.5.4") {
            exclude(group: "junit")
        }

        testCompile "org.junit.jupiter:junit-jupiter-api:5.3.1"
        testRuntime "org.junit.jupiter:junit-jupiter-engine:5.3.1"
    }

    compileGroovy {
        groovyOptions.optimizationOptions.indy = true
    }

    test {
        useJUnitPlatform()
    }

    jacocoTestReport {
        reports {
            xml.enabled = true // coveralls plugin depends on xml format report
            html.enabled = true
        }
    }

    jacoco {
        toolVersion = "0.8.2"
        reportsDir = file("$buildDir/customJacocoReportDir")
    }
}

